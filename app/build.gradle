apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    defaultConfig {
        resConfigs 'en'
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 40302   // Read 4.xx.yy to match version name
        versionName '4.3.2'

        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    /*
     * To sign release build, create file keystore.properties in root of this project with this content:
     *
     * storeFile=/home/key.store
     * storePassword=xxx
     * keyAlias=alias
     * keyPassword=xxx
     */
    if (keystorePropertiesFile.exists()) {
        println "Found signature properties file. Build will be signed."
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        signingConfigs {
            release {
                storeFile rootProject.file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Do not abort build if lint finds errors
    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext {
    appcompat = '1.2.0-alpha03'
    constraint_layout = '2.0.0-beta4'
    lifecycle = '2.2.0'
    material = '1.2.0-alpha05'
    room = '2.2.4'
    support = '1.0.0'
    work = '2.3.2'
    nav_version = '2.1.0-alpha05'
}

dependencies {
    implementation project(':libraries:RootCommands')
    implementation project(':tcpdump')
    implementation project(':webserver')

    // Architecture components
    implementation "androidx.lifecycle:lifecycle-extensions:${lifecycle}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${lifecycle}"
    implementation "androidx.room:room-runtime:${room}"
    annotationProcessor "androidx.room:room-compiler:${room}"
    implementation "androidx.work:work-runtime:${work}"

    // Support libraries
    implementation "androidx.appcompat:appcompat:${appcompat}"
    implementation "androidx.cardview:cardview:${support}"
    implementation "androidx.constraintlayout:constraintlayout:${constraint_layout}"
    implementation "androidx.drawerlayout:drawerlayout:1.1.0-alpha03"
    implementation "androidx.preference:preference:1.1.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0-alpha03"
    implementation "androidx.fragment:fragment:1.2.2"
    implementation "com.google.android.material:material:${material}"

    // Miscellaneous
    api 'com.google.guava:guava:28.2-android'
    implementation 'com.annimon:stream:1.2.1'
    implementation 'com.google.android:flexbox:2.0.1'
    implementation 'com.squareup.okhttp3:okhttp:4.4.0'
    implementation 'net.sf.trove4j:trove4j:3.0.3'
    implementation 'org.sufficientlysecure:html-textview:3.9'
    releaseImplementation project(':sentrystub')
    debugImplementation 'io.sentry:sentry-android:1.7.30'
    implementation 'org.slf4j:slf4j-nop:2.0.0-alpha1'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.json:json:20190722'
}
