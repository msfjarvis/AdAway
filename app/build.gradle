apply plugin: 'com.android.application'

repositories {
    maven {
        url 'https://jitpack.io'
    }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'
    ndkVersion '21.3.6528147'

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 50007   // Read x.yy.zz to match version name
        versionName '5.0.7'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    /*
     * To sign release build, create file keystore.properties in root of this project with this content:
     *
     * storeFile=/home/key.store
     * storePassword=xxx
     * keyAlias=alias
     * keyPassword=xxx
     */
    if (keystorePropertiesFile.exists()) {
        println "Found signature properties file. Build will be signed."
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        signingConfigs {
            release {
                storeFile rootProject.file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    buildFeatures {
        viewBinding = true
    }

    buildTypes {
//        debug {
//            shrinkResources false
//            minifyEnabled false
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Do not abort build if lint finds errors
    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    // Native modules
    implementation project(':tcpdump')
    implementation project(':webserver')

    // AndroidX components
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0'
    implementation 'androidx.fragment:fragment:1.2.5'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.paging:paging-runtime:2.1.2'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.room:room-runtime:2.2.5'
    annotationProcessor 'androidx.room:room-compiler:2.2.5'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime:2.4.0'
    implementation 'com.google.android.material:material:1.2.0'

    // Miscellaneous
    implementation 'com.google.guava:guava:29.0-android'
    implementation 'com.annimon:stream:1.2.1'
    implementation 'com.squareup.okhttp3:okhttp:3.14.7'
    implementation 'org.sufficientlysecure:html-textview:3.9'
    debugImplementation project(':sentrystub')
    releaseImplementation 'io.sentry:sentry-android:1.7.30'

    // Root related
    implementation 'com.github.topjohnwu.libsu:core:2.5.1'
    implementation 'com.github.topjohnwu.libsu:io:2.5.1'

    // VPN related
    implementation 'org.pcap4j:pcap4j-core:1.8.2'
    implementation 'org.pcap4j:pcap4j-packetfactory-static:1.8.2'
    implementation 'dnsjava:dnsjava:3.0.2'
    implementation 'org.slf4j:slf4j-android:1.7.30'

    // Test related
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.json:json:20190722'
    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'junit:junit:4.13'
}
